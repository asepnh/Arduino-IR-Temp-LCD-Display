/*************************************************** 
Project to display temperature from an IR temperature sensor on a 16x2 LCD display

Hardware list :

Cytron Maker Uno
MLX IR Temp Sensor - Connected to SCL SDA
1602 LCD Display - Connected to SCL SDA
Microwave Radar - Connected to pin 3
IR Digital Sensor - Connected to pin 4

By : ROBOTIK SIKL

 ****************************************************/

#include <Wire.h>
#include <Adafruit_MLX90614.h>
#include <LiquidCrystal_I2C.h>

Adafruit_MLX90614 mlx = Adafruit_MLX90614();

LiquidCrystal_I2C lcd(0x27,20,4);  // set the LCD address to 0x27 for a 16 chars and 2 line display

int pinRadar = 3;         // radar is connected to pin 3
int pinIRdistance = 4;     // IR distance is connected to pin 4

void setup() {

  // Initialize serial communication
  Serial.begin(9600);

  // Initialize the temperature sensor
  Serial.println("Adafruit MLX90614 test");  
  mlx.begin();  

  // Initialize the LCD
  lcd.init();                      
  lcd.backlight();        // turn on backlight
  lcd.setCursor(2,0);
  lcd.print("Robotik SIKL");
  lcd.setCursor(1,1);
  lcd.print("IR Thermometer");
  delay (3000);
  lcd.noBacklight();      // turn off backlight
  lcd.clear();          // clear the LCD
  
  // Make the radar pin an input:
  pinMode(pinRadar, INPUT);
  pinMode(pinIRdistance, INPUT);
  
}


void loop() {

  // Read the radar sensor input
  int radarDetect = digitalRead(pinRadar);
  int IRdistance = digitalRead(pinIRdistance);

  // If radar detects movement, turn on the LED & display temperature value. If not, turn off LED & clear LCD.
  if (radarDetect) {
    lcd.backlight();      // turn on backlight
    
    if (IRdistance == LOW) {
    
      lcd.setCursor(0,0);
      lcd.print("Ambient ");
      lcd.print(mlx.readAmbientTempC());
      lcd.print(" C");
 
      lcd.setCursor(0,1);
      lcd.print("Target  ");
      lcd.print(mlx.readObjectTempC());
      lcd.print(" C");
    
      delay(5000);
      lcd.clear();          // clear the LCD
      
    } else {
      
      lcd.setCursor(0,0);      
      lcd.print("Silahkan Ukur");
      lcd.setCursor(0,1);      
      lcd.print("Suhu Tubuh Anda");
      
    }
 
  } else {  
    lcd.noBacklight();    // turn off backlight

  }


}
